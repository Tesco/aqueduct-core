import org.gradle.internal.os.OperatingSystem;

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

distZip.enabled = false
distTar.enabled = false

mainClassName = 'com.tesco.aqueduct.pipe.http.PipeCloudServer'

dependencies {
    compile project (":pipe-api")
    compile project (":pipe-http-server")
    compile project (":pipe-storage-postgresql")
    compile project (":registry-http-server")
    compile project (":telemetry")

    addMicronautDependencies()

    compile 'io.micronaut:micronaut-http-client'
    compile 'com.sun.activation:javax.activation:1.2.0'

    compile 'com.nixxcode.jvmbrotli:jvmbrotli:0.2.0'
    // it would be nicer if done as optional
    compile 'com.nixxcode.jvmbrotli:jvmbrotli-linux-x86-amd64:0.2.0'
    testCompile 'com.nixxcode.jvmbrotli:jvmbrotli-win32-x86-amd64:0.2.0'
    testCompile 'com.nixxcode.jvmbrotli:jvmbrotli-darwin-x86-amd64:0.2.0'

    testCompile 'com.opentable.components:otj-pg-embedded:0.13.0'
    testCompile project(":pipe-storage-memory")
}

jar {
    manifest {
        attributes 'Specification-Version': scmVersion.version
        attributes 'Specification-Vendor': 'Tesco PLC ltd.'
        attributes 'Implementation-Title': 'com.tesco.aqueduct.pipe'
        attributes 'Implementation-Version': scmVersion.version
    }
}

task createVersionFile() {
    doFirst {
        // for jenkins pipeline
        new File("VERSION.txt").text = scmVersion.version
    }
}

processResources.dependsOn createVersionFile

shadowJar {
    mergeServiceFiles()
}

addPublish()
